PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

select distinct ?property_prefix (count(?property_prefix) as ?count)
where {
    {
        ?property rdf:type owl:ObjectProperty .
    } union {
        ?property rdf:type owl:DatatypeProperty .
    } union {
        ?property rdf:type owl:AnnotationProperty .
    }
   
    {
        bind(strbefore(strafter(str(?property), "http://purl.obolibrary.org/obo/"), "_") AS ?property_prefix)
    } union {
        bind(strafter(str(?property), "http://www.ebi.ac.uk/") AS ?property_prefix)
    }
    filter(strlen(str(?property_prefix)) > 0)

} group by ?property_prefix
order by desc(?count) ?property_prefix